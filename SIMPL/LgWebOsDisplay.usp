#SYMBOL_NAME "LG WebOS Display"

#INCLUDEPATH "D:\\Programming\\Crestron Modules\\Mat\\SIMPLSharp Projects\\LgWebOs\\LgWebOs\\LgWebOs\\bin\\Debug"
#USER_SIMPLSHARP_LIBRARY "LgWebOs"

#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE

Digital_Input PowerOn, PowerOff, _skip_, VolumeUp, VolumeDown, VolumeMuteOn, VolumeMuteOff;
Digital_Input _skip_, KeyUp, KeyDown, KeyLeft, KeyRight, KeyHome, KeyBack, KeySelect, KeyExit, KeyInfo, KeyRed, KeyGreen, KeyYellow, KeyBlue, KeyPlay, KeyPause, KeyStop;
Digital_Input _skip_, GetInputs;
Analog_Input ChangeInput;

Digital_Output PowerIsOn, PowerIsOff, _skip_, MuteIsOn, MuteIsOff;
Analog_Output VolumeValue;
Analog_Output _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_,_skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, CurrentInput;
String_Output InputNamesXsig, InputIconsXsig;

Display webOsDisplay;

String_Parameter _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, ID[100], IpAddress[100], MacAddress[100];
Integer_Parameter Port, VolumeRepeatTime;

#BEGIN_PARAMETER_PROPERTIES Port
    propDefaultValue=3000d;
#END_PARAMETER_PROPERTIES

integer newDirection;

function VolumeRepeat(integer direction)
{
	newDirection = direction;

    if(newDirection = 1)
    	webOsDisplay.IncrementVolume();
    else if(newDirection = 0)
        webOsDisplay.DecrementVolume();

    wait(VolumeRepeatTime, vRepeat)
    {
        VolumeRepeat(newDirection);
    }
}

Push PowerOn
{
    webOsDisplay.PowerOn();
}

Push PowerOff
{
    webOsDisplay.PowerOff();
}

Push VolumeUp
{
    VolumeRepeat(1);
}

Release VolumeUp
{
    CancelWait(vRepeat);
}

Push VolumeDown
{
    VolumeRepeat(0);
}

Release VolumeDown
{
    CancelWait(vRepeat);
}

Push VolumeMuteOn
{
    webOsDisplay.SetMute(1);
}

Push VolumeMuteOff
{
    webOsDisplay.SetMute(0);
}

Push KeyUp
{
    webOsDisplay.SendKey("UP");
}

Push KeyDown
{
    webOsDisplay.SendKey("DOWN");
}

Push KeyLeft
{
    webOsDisplay.SendKey("LEFT");
}

Push KeyRight
{
    webOsDisplay.SendKey("RIGHT");
}

Push KeyHome
{
    webOsDisplay.SendKey("HOME");
}

Push KeyBack
{
    webOsDisplay.SendKey("BACK");
}

Push KeySelect
{
    webOsDisplay.SendKey("ENTER");
}

Push KeyExit
{
    webOsDisplay.SendKey("EXIT");
}

Push KeyInfo
{
    webOsDisplay.SendKey("INFO");
}

Push KeyRed
{
    webOsDisplay.SendKey("RED");
}

Push KeyGreen
{
    webOsDisplay.SendKey("GREEN");
}

Push KeyYellow
{
    webOsDisplay.SendKey("YELLOW");
}

Push KeyBlue
{
    webOsDisplay.SendKey("BLUE");
}

Push KeyPlay
{
    webOsDisplay.SendKey("PLAY");
}

Push KeyPause
{
    webOsDisplay.SendKey("PAUSE");
}
Push KeyStop
{
    webOsDisplay.SendKey("STOP");
}

Push GetInputs
{
    webOsDisplay.GetInputs();
}

Change ChangeInput
{
    webOsDisplay.ChangeInput(ChangeInput);
}

callback function newPowerState(integer state)
{
    if(state =1)
    {
        PowerIsOn = ON;
        PowerIsOff = OFF;
    }
    else
    {
        PowerIsOn = OFF;
        PowerIsOff = ON;
    }
}

callback function newVolumeValue(integer value)
{
    VolumeValue = value;
}

callback function newVOlumeMuteState(integer state)
{
    if(state = 1)
    {
        MuteIsOn = ON;
        MuteIsOff = OFF;
    }
    else
    {
        MuteIsOn = OFF;
        MuteIsOff = ON;
    }
}

callback function newCurrentInputValue(integer value)
{
    CurrentInput = value + 1;
}

callback function newExternalInputNames(string xsig)
{
    InputNamesXsig = xsig;
}

callback function newExternalInputIcons(string xsig)
{
    InputIconsXsig = xsig;
}

Function Main()
{
    WaitForInitializationComplete();

    RegisterDelegate(webOsDisplay, onPowerState, newPowerState);
    RegisterDelegate(webOsDisplay, onVolumeValue, newVolumeValue);
    RegisterDelegate(webOsDisplay, onVolumeMuteState, newVolumeMuteState);
    RegisterDelegate(webOsDisplay, onCurrentInputValue, newCurrentInputValue);
    RegisterDelegate(webOsDisplay, onExternalInputNames, newExternalInputNames);
    RegisterDelegate(webOsDisplay, onExternalInputIcons, newExternalInputIcons);


    webOsDisplay.Initialize(ID, IpAddress, Port, MacAddress);
}